<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="Forum.TopicsLoader">
<Description>
Initial topic loader </Description>
<Super>Forum.TopicsPage</Super>
<TimeCreated>63776,59960.045951</TimeCreated>

<Method name="Load">
<Description>
Entry point</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>forum="cache"</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	s sc = 1

	if ##class(Forum).NameExists( forum, .forumId ) {
		
		#;s sc = ##class(Forum).NameDelete( forum ) Q:'sc sc
		#; 5803,"Forum.Topic" ERROR #5803: Can't get exclusive lock
		#; workaround:
		w !, "Delete old posts..." 
				
		s sql = "Select ID From Forum.Post Where Topic->Forum = ?"
		s rs = ##class(%SQL.Statement).%ExecDirect( , sql, forumId)
		while rs.%Next() {
			s sc = sc && ##class(Forum.Post).%DeleteId( rs.ID )
			Q:'sc	
		}
		
		w " Deleted! ", sc
		Q:'sc sc 
		
		w !, "Delete old topics..." 
		s sql = "Select ID From Forum.Topic Where Forum = ?"
		s rs = ##class(%SQL.Statement).%ExecDirect( , sql, forumId)
		while rs.%Next(){
			s sc = sc && ##class(Forum.Topic).%DeleteId( rs.ID )
			Q:'sc	
		}
		w " Deleted! ", sc,!
		Q:'sc sc 
		
	} else {
		
		k data s data("Name") = forum 
		w !, "Create new forum: ", forum 
		s sc = ##class(Forum).Insert( .data ) 
		w " Created! ", sc
		Q:'sc sc
		
	}
	
	Q ..Scan( forum )
]]></Implementation>
</Method>

<Method name="Scan">
<Description>
Scan across all forum topic pages</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>forum="cache"</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	
	s i = 1, sc = 1
	
	s page = ..GetPage( forum, i, .sc ) Q:'sc
	s count = page.Pages() ; get pages count
	s sc = page.Parse() Q:'sc
	
	for {
		
		w !, "Page #"_i_" successfuly loaded", !
		s i = i + 1  if i > count Q
		s sc = page.Get( , i ) Q:'sc ;load page #i in parser  
		s sc = page.Parse() Q:'sc
		
	}
	Q sc
]]></Implementation>
</Method>

<Method name="OnTopic">
<Description><![CDATA[
Event handler 
<var>data</var> - key-value array with topic properties 
If handler return 0, parsing will be interrupted]]></Description>
<FormalSpec><![CDATA[&data]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	
	Q:'##class(Forum).NameExists( ..Forum, .forumId ) 0
	s data("Forum") = forumId

	s topic = $g(data("ExtId")), amount = $g(data("PagesAmount"))

	s sc = ##class(Topic).Insert( .data ) 
	#; any TopicsPage may have repeated first topic 
	if 'sc && ( $system.Status.GetErrorCodes( sc ) = 5808 ) {
		Q 1
	}

	w $g(data), " " ;output topic id (not ExtId) 
	
	k args s args( "topic" ) = topic, args( "pages" ) = amount 
	Q ##class(PostsLoader).Load( .args )
]]></Implementation>
</Method>
</Class>
</Export>
